#! /bin/csh -f
#
#  grab homeworks for all ASTR288P students and place them in locally uniq names for grading
#

#  skip "teuben288"
set students = (aleis288  eschwart  eyates   nanthony  rpopescu cmr221    ewilliam  jcorb288  mvarakia  qgonzale)

set machine = ursa.astro.umd.edu
set home    = /ursa/A288P
set hw      = Homework-02


# accumulators for reporting
set hw_0 = ()     # the ones who has not sent in homework
set hw_1 = ()     # the ones who have sent in homework

foreach s ($students)
  echo -n "$s "
  scp -p ${machine}:${home}/$s/$hw.ipynb ${hw}_${s}.ipynb >& /dev/null
  if ($status) then
    set hw_0 = ($hw_0 $s)
  else
    set hw_1 = ($hw_1 $s)
  endif
end


echo ""
echo ""
echo "HOME ok: [$#hw_1] $hw_1"
echo "HOME missing: [$#hw_0] $hw_0"
